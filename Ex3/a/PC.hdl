// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=regRes, out=incRes);
    Mux16(a=regRes, b=incRes, sel=inc, out=incMuxRes);
    Mux16(a=incMuxRes, b=in, sel=load, out=loadRes);
    Mux16(a=loadRes, b=false, sel=reset, out=resetRes);
    
    Register(in=resetRes , load=true , out=regRes, out=out);
}
